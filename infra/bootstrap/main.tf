data "aws_caller_identity" "current" {}

locals {
  tfstate_bucket_name = "${var.project}-tfstate-${data.aws_caller_identity.current.account_id}"
  tflock_table_name   = "${var.project}-tflock"
}

resource "aws_s3_bucket" "tfstate" {
  bucket = local.tfstate_bucket_name

  # 실수로 destroy 되어도 객체가 남아있으면 destroy 차단 (state 손실 방지)
  lifecycle {
    prevent_destroy = true
  }
}

resource "aws_s3_bucket_versioning" "tfstate" {
  bucket = aws_s3_bucket.tfstate.id

  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "tfstate" {
  bucket = aws_s3_bucket.tfstate.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

resource "aws_s3_bucket_public_access_block" "tfstate" {
  bucket = aws_s3_bucket.tfstate.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_dynamodb_table" "tflock" {
  name         = local.tflock_table_name
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "LockID"

  attribute {
    name = "LockID"
    type = "S"
  }
}
